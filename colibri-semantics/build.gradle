description = 'Semantic core bundle for Colibri project'

eclipse
{
	project 
	{
		natures 'org.eclipse.pde.PluginNature'
		buildCommand 'org.eclipse.pde.ManifestBuilder'
		buildCommand 'org.eclipse.pde.SchemaBuilder'
	}
	
	classpath
	{
		file
		{
			beforeMerged 
			{ 
				classpath -> classpath.entries.removeAll { entry -> entry.kind == 'con' } 
			}
			withXml
			{
				def node = it.asNode()
				node.appendNode('classpathentry',[kind: 'con', path: 'org.eclipse.pde.core.requiredPlugins'])
			}
		}
	}
}

configurations 
{
	provided
}

sourceSets
{
	main { compileClasspath += configurations.provided }
}

jar 
{
	from 
	{ 
		configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } 
	}
	
	manifest 
	{
		attributes (   
			"Manifest-Version": "1.0",
			"Bundle-ManifestVersion": "2",
			"Bundle-Name": "colibri-semantics",
			"Bundle-SymbolicName": "colibri-semantics",
			"Bundle-Version": "1.0.0",
			"Bundle-Activator": "at.ac.tuwien.auto.colibri.semantics.SemanticsBundleActivator",
			"Bundle-Vendor": "Automation Systems Group, Institute of Computer Aided Automation, TU Wien, www.auto.tuwien.ac.at",
			"Export-Package": "at.ac.tuwien.auto.colibri.semantics",
			"Import-Package": "at.ac.tuwien.auto.colibri.data, org.apache.jena;version='3.0.1', org.osgi.framework;version='1.3.0'"
		)
	}
}

dependencies 
{		
	provided group: 'org.apache.commons', name: 'commons-jexl', version: '2.1.1'
	
	compile group:'org.apache.jena', name:'jena-core', version:'3.0.1'
	compile group:'org.apache.jena', name:'jena-tdb', version:'3.0.1'
}
